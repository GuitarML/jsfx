desc: Dual Distortion (Geraint Luff)

slider1:-24<-60,12,0.1>-upper threshhold (dB)
slider2:2<1,10,0.01>-upper ratio
slider3:-32<-60,12,0.1>-lower threshhold (dB)
slider4:6<1,10,0.01>-lower ratio
slider5:0<-60,40,0.1>-extra gain (dB)

import ui-lib.jsfx-inc

@init

ui_setup(0);

function calculate_params() (
  upperthreshhold = 10 ^ (slider1/20); 
  upperratio = 1/slider2;
  uppermultiplier = upperthreshhold/(upperthreshhold ^ upperratio);
  
  lowerthreshhold = -(10 ^ (slider3/20));
  lowerratio = 1/slider4;
  lowermultiplier = lowerthreshhold/(abs(lowerthreshhold) ^ lowerratio);
  
  gain = 10 ^ (slider5/20);
);

@slider

calculate_params();

@sample

(spl0 > upperthreshhold) ? (
  spl0 = uppermultiplier*(spl0 ^ upperratio);
);
(spl0 < lowerthreshhold) ? (
  spl0 = lowermultiplier*((-spl0) ^ lowerratio);
);

(spl1 > upperthreshhold) ? (
  spl1 = uppermultiplier*(spl1 ^ upperratio);
);
(spl1 < lowerthreshhold) ? (
  spl1 = lowermultiplier*((-spl1) ^ lowerratio);
);

spl0 = gain*spl0;
spl1 = gain*spl1;

@gfx 475 370

function labelled_dial(value, default, low, high, bias, label, display) (
  ui_push_height(50);
    value = control_dial(value, low, high, bias);
    ui_click() && ui_clickcount() == 2 ? (
      value = default;
    );
  ui_pop();
  ui_push_below(50);
    ui_align(0.5, 0);
    ui_text(label);
  ui_pop();
  ui_push_above(50);
    ui_align(0.5, 1);
    ui_textnumber(value, display);
  ui_pop();
  value;
);

ui_start("main");

ui_screen() == "main" ? (
  control_navbar("Dual Distortion", -1, -1);
  
  ui_split_top(120);
    ui_split_leftratio(2/5);
      control_group("Upper limit");
      ui_split_leftratio(1/2);
        slider1 = labelled_dial(slider1, -24, -60, 12, 1, "threshhold", "%0.1fdB");
      ui_split_next();
        slider2 = labelled_dial(slider2, 2, 1, 10, 1, "ratio", "%0.1f");
      ui_pop();
    ui_split_next(2/5);
      control_group("Lower limit");
      ui_split_leftratio(1/2);
        slider3 = labelled_dial(slider3, -32, -60, 12, 1, "threshhold", "%0.1fdB");
      ui_split_next();
        slider4 = labelled_dial(slider4, 6, 1, 10, 1, "ratio", "%0.1f");
      ui_pop();
    ui_pop();
    slider5 = labelled_dial(slider5, 0, -60, 40, 0, "gain", "%0.1fdB");
  ui_pop();
  
  ui_color(0, 0, 0);
  ui_fill();

  ui_color(0, 255, 0);
  gi = 0;
  gN = ui_width();
  while (gi < gN) (
    gx = ui_left() + gi;
    gv = (gi/gN)*2 - 1;
    gv > upperthreshhold ? (
      gv = uppermultiplier*(gv^upperratio);
    );
    gv < lowerthreshhold ? (
      gv = lowermultiplier*((-gv)^lowerratio);
    );
    gv *= gain;
    gv = max(-1, min(1, gv));
    gy = ui_top() + (0.5 - 0.5*gv)*ui_height();
    gi > 0 ? (
      gfx_lineto(gx, gy);
    ) : (
      gfx_x = gx;
      gfx_y = gy;
    );
    gi += 1;
  );
  
  control_finish_enabled(1);
) : ui_system();

calculate_params();
