desc:Spring-Box Delay-Reverb (by Geraint Luff)

in_pin:Ch1
in_pin:Ch2
out_pin:Ch1
out_pin:Ch2

slider1:50<1,1000,1>-density (ms)
slider2:60<0,99,1>-feedback
slider3:80<0,95,1>-late-bias (%)
slider4:50<0,100,1>-wet (%)
slider5:100<0,100,1>-dry (%)
slider6:0<0,1,1{even,long}>-room shape
slider7:8000<100,20000,1>-filter (lowpass Hz)
slider8:0<0,100,1>-detuning (cents)
slider9:2<0.1,10,0.1>-chorus speed (Hz)

import ui-lib.jsfx-inc

@init

freemem = ui_setup(0);
function alloc(N) local(result, i) (
	result = freemem;
	i = 0;
	while (i < N) (
		result[i] = 0;
	);
	freemem += N;
	result;
);

maxdetunefactor = pow(2, 110/1200)*exp(0.15); // 110 cents plus variation
mindetunefreq = 0.1;
buffersize = ceil(srate*exp(0.3) + maxdetunefactor/mindetunefreq/(2*$pi)*srate + 1); // Longest possible delay, plus 100 samples
buffer1a = alloc(buffersize);
buffer1b = alloc(buffersize);
buffer2a = alloc(buffersize);
buffer2b = alloc(buffersize);
buffer3a = alloc(buffersize);
buffer3b = alloc(buffersize);
buffer4a = alloc(buffersize);
buffer4b = alloc(buffersize);

filterLx1 = filterLx2 = 0;
filterRx1 = filterRx2 = 0;
filterLy1 = filterLy2 = 0;
filterRy1 = filterRy2 = 0;

writepos = 0;
delaysamples1 = delaysamples2 = delaysamples3 = delaysamples4 = 0;
chorusphase1 = rand()*2*$pi;
chorusphase2 = rand()*2*$pi;
chorusphase3 = rand()*2*$pi;
chorusphase4 = rand()*2*$pi;

function update() local(delay, chorus, chorusfactor) (
	delayfactor1 = exp(0.3);
	delayfactor2 = exp(0.1);
	delayfactor3 = exp(-0.1);
	delayfactor4 = exp(-0.3);

	slider6 == 1 ? (
		delayfactor1 = exp(0.0);
		delayfactor2 = exp(0.04);
		delayfactor3 = exp(-0.09);
		delayfactor4 = exp(0.13);
	);

	delay = slider1*srate/1000;
	delaysamples1 = min(buffersize - 1, delay*delayfactor1);
	delaysamples2 = min(buffersize - 1, delay*delayfactor2);
	delaysamples3 = min(buffersize - 1, delay*delayfactor3);
	delaysamples4 = min(buffersize - 1, delay*delayfactor4);
	delaysamples1i = ceil(delaysamples1);
	delaysamples2i = ceil(delaysamples2);
	delaysamples3i = ceil(delaysamples3);
	delaysamples4i = ceil(delaysamples4);
	
	chorus = slider9*2*$pi/srate;
	chorusphasespeed1 = chorus*exp(-0.15);
	chorusphasespeed2 = chorus*exp(-0.05);
	chorusphasespeed3 = chorus*exp(0.05);
	chorusphasespeed4 = chorus*exp(0.15);
	chorusfactor = pow(2, slider8/1200) - 1;
	chorussamples = chorusfactor*srate/(slider9*2*$pi);

	feedbackA = slider2/100;
	feedbackRatio = slider3/100;
	feedbackB = feedbackA*feedbackRatio;
	dry = slider5/100;
	wet = slider4/100;
	// Feedback-racing compensation
	amp = (1 - feedbackA*feedbackRatio)/(1 - feedbackRatio);
	
	ampB = amp*min(1, feedbackRatio*1.1);
);

coeff_a = 1;
coeff_b = exp(0);
coeff_c = -coeff_a;
coeff_d = 1/coeff_b;

coeff_scale = sqrt(coeff_a*coeff_a + coeff_b*coeff_b + coeff_c*coeff_c + coeff_d*coeff_d);
coeff_a /= coeff_scale;
coeff_b /= coeff_scale;
coeff_c /= coeff_scale;
coeff_d /= coeff_scale;

mat11 = coeff_a;
mat12 = coeff_b;
mat13 = coeff_c;
mat14 = coeff_d;
mat21 = -coeff_d;
mat22 = -coeff_a;
mat23 = -coeff_b;
mat24 = -coeff_c;
mat31 = -coeff_c;
mat32 = -coeff_d;
mat33 = -coeff_a;
mat34 = -coeff_b;
mat41 = coeff_b;
mat42 = coeff_c;
mat43 = coeff_d;
mat44 = coeff_a;

0 ? (
	matA = 1;
	matB = 1;
	matC = -1;
	matScaling = 1/sqrt(matA*matA + matB*matB + matC*matC);
	matA *= matScaling;
	matB *= matScaling;
	matC *= matScaling;
	
	mat11 = 0; mat12 = matA; mat13 = matB; mat14 = matC;
	mat21 = -matA; mat22 = 0; mat23 = -matC; mat24 = matB;
	mat31 = matB; mat32 = -matC; mat33 = 0; mat34 = matA;
	mat41 = matC; mat42 = matB; mat43 = -matA; mat44 = 0;
);

@slider

update();

@block

@sample

chorusphase1 += chorusphasespeed1;
chorusphase1 > 2*$pi ? chorusphase1 -= 2*$pi;
chorusphase2 += chorusphasespeed2;
chorusphase2 > 2*$pi ? chorusphase2 -= 2*$pi;
chorusphase3 += chorusphasespeed3;
chorusphase3 > 2*$pi ? chorusphase3 -= 2*$pi;
chorusphase4 += chorusphasespeed4;
chorusphase4 > 2*$pi ? chorusphase4 -= 2*$pi;

1 ? (
	// Linearly interpolated delay
	d = delaysamples1 + (1 + sin(chorusphase1))*0.5*chorussamples;
	di = floor(d);
	dr = d - di;
	dr *= dr*(0.5 - dr*0.3333);
	readpos1 = writepos - di;
	readpos1 < 0 ? readpos1 += buffersize;
	readpos2 = writepos - di - 1;
	readpos2 < 0 ? readpos2 += buffersize;
	echo1a = buffer1a[readpos1] + (buffer1a[readpos2] - buffer1a[readpos1])*dr;
	echo1b = buffer1b[readpos1] + (buffer1b[readpos2] - buffer1b[readpos1])*dr;

	d = delaysamples2 + (1 + sin(chorusphase2))*0.5*chorussamples;
	di = floor(d);
	dr = d - di;
	dr *= dr*(0.5 - dr*0.3333);
	readpos1 = writepos - di;
	readpos1 < 0 ? readpos1 += buffersize;
	readpos2 = writepos - di - 1;
	readpos2 < 0 ? readpos2 += buffersize;
	echo2a = buffer2a[readpos1] + (buffer2a[readpos2] - buffer2a[readpos1])*dr;
	echo2b = buffer2b[readpos1] + (buffer2b[readpos2] - buffer2b[readpos1])*dr;

	d = delaysamples3 + (1 + sin(chorusphase3))*0.5*chorussamples;
	di = floor(d);
	dr = d - di;
	dr *= dr*(0.5 - dr*0.3333);
	readpos1 = writepos - di;
	readpos1 < 0 ? readpos1 += buffersize;
	readpos2 = writepos - di - 1;
	readpos2 < 0 ? readpos2 += buffersize;
	echo3a = buffer3a[readpos1] + (buffer3a[readpos2] - buffer3a[readpos1])*dr;
	echo3b = buffer3b[readpos1] + (buffer3b[readpos2] - buffer3b[readpos1])*dr;

	d = delaysamples4 + (1 + sin(chorusphase4))*0.5*chorussamples;
	di = floor(d);
	dr = d - di;
	dr *= dr*(0.5 - dr*0.3333);
	readpos1 = writepos - di;
	readpos1 < 0 ? readpos1 += buffersize;
	readpos2 = writepos - di - 1;
	readpos2 < 0 ? readpos2 += buffersize;
	echo4a = buffer4a[readpos1] + (buffer4a[readpos2] - buffer4a[readpos1])*dr;
	echo4b = buffer4b[readpos1] + (buffer4b[readpos2] - buffer4b[readpos1])*dr;
) : (
	readpos = writepos - delaysamples1i;
	readpos < 0 ? readpos += buffersize;
	echo1a = buffer1a[readpos];
	echo1b = buffer1b[readpos];

	readpos = writepos - delaysamples2i;
	readpos < 0 ? readpos += buffersize;
	echo2a = buffer2a[readpos];
	echo2b = buffer2b[readpos];

	readpos = writepos - delaysamples3i;
	readpos < 0 ? readpos += buffersize;
	echo3a = buffer3a[readpos];
	echo3b = buffer3b[readpos];

	readpos = writepos - delaysamples4i;
	readpos < 0 ? readpos += buffersize;
	echo4a = buffer4a[readpos];
	echo4b = buffer4b[readpos];
);

y1a = echo1a*mat11 + echo2a*mat12 + echo3a*mat13 + echo4a*mat14;
y1b = echo1b*mat11 + echo2b*mat12 + echo3b*mat13 + echo4b*mat14;
y2a = echo1a*mat21 + echo2a*mat22 + echo3a*mat23 + echo4a*mat24;
y2b = echo1b*mat21 + echo2b*mat22 + echo3b*mat23 + echo4b*mat24;
y3a = echo1a*mat31 + echo2a*mat32 + echo3a*mat33 + echo4a*mat34;
y3b = echo1b*mat31 + echo2b*mat32 + echo3b*mat33 + echo4b*mat34;
y4a = echo1a*mat41 + echo2a*mat42 + echo3a*mat43 + echo4a*mat44;
y4b = echo1b*mat41 + echo2b*mat42 + echo3b*mat43 + echo4b*mat44;

left = spl0;
right = spl1;

buffer1a[writepos] = y1a*feedbackA + left*amp;
buffer1b[writepos] = y1b*feedbackB + left*ampB;
buffer2a[writepos] = y2a*feedbackA + right*amp;
buffer2b[writepos] = y2b*feedbackB + right*ampB;
buffer3a[writepos] = y3a*feedbackA + left*amp;
buffer3b[writepos] = y3b*feedbackB + left*ampB;
buffer4a[writepos] = y4a*feedbackA + right*amp;
buffer4b[writepos] = y4b*feedbackB + right*ampB;

left = (y1a - y1b + y3a - y3b)*0.71;
right = (y2a - y2b + y4a - y4b)*0.71;

// Filter
filter_Q = 0.5;
filter_freq = slider7;
filter_w0 = 2*$pi*min(0.49, filter_freq/srate);
filter_cosW0 = cos(filter_w0);
filter_alpha = sin(filter_w0)/(2*filter_Q);

filter_a0 = 1 + filter_alpha;
filter_a1 = (-2*filter_cosW0)/filter_a0;
filter_a2 = (1 - filter_alpha)/filter_a0;
filter_b1 = (1 - filter_cosW0)/filter_a0;
filter_b0 = filter_b1/2;
filter_b2 = filter_b0;

x = left;
y = filter_b0*x + filter_b1*filterLx1 + filter_b2*filterLx2 - filter_a1*filterLy1 - filter_a2*filterLy2;
filterLx2 = filterLx1;
filterLx1 = x;
filterLy2 = filterLy1;
filterLy1 = y;
left = y;

x = right;
y = filter_b0*x + filter_b1*filterRx1 + filter_b2*filterRx2 - filter_a1*filterRy1 - filter_a2*filterRy2;
filterRx2 = filterRx1;
filterRx1 = x;
filterRy2 = filterRy1;
filterRy1 = y;
right = y;

spl0 = spl0*dry + left*wet;
spl1 = spl1*dry + right*wet;

writepos += 1;
writepos >= buffersize ? writepos = 0;

@gfx 510 360

function labelled_dial(value, low, high, bias, label, number_format) (
	ui_push_height(50);		
		value = control_dial(value, low, high, bias);
	ui_pop();
	ui_push_above(50);
		ui_align(0.5, 0.85);
		ui_text(label);
	ui_pop();
	ui_push_below(50);
		ui_align(0.5, 0.15);
		number_format >= 0 ? (
			ui_textnumber(value, number_format);
		);
	ui_pop();
	value;
);

control_start("main", "default");

ui_screen() == "main" ? (
	control_navbar("Spring-Box Delay", -1, -1);
	ui_split_rightratio(0.7);
		ui_split_topratio(0.55);
			control_group("Delay");
			ui_split_bottomratio(0.3);
				ui_split_right(150);
					ui_pad();
					ui_push_heighttext(-1);
						slider6 = control_selector(slider6, slider6 ? "clustered" : "smooth", 1 - slider6, 1 - slider6);
					ui_pop();
				ui_pop();
		
				ui_align(1, 0.5);
				ui_text("echoes:");
			ui_pop();

			ui_split_leftratio(1/3);
				slider1 = labelled_dial(slider1, 1, 1000, 4, "density", "%ims");
			ui_split_next();
				slider2 = labelled_dial(slider2, 0, 99, -1, "feedback", "%i%%");
			ui_split_next();
				slider3 = labelled_dial(slider3, 0, 95, 0, "late-bias", "%i%%");
			ui_pop();
		ui_split_next();
			control_group("Output");
			ui_split_leftratio(1/3);
				slider4 = labelled_dial(slider4, 0, 100, 0, "wet", "%i%%");
			ui_split_next();
				slider5 = labelled_dial(slider5, 0, 100, 0, "dry", "%i%%");
			ui_split_next();
				slider7 = labelled_dial(slider7, 100, 20000, 2, "filter", "%iHz");
			ui_pop();
		ui_pop();
	ui_split_next();
		control_group("Chorus");
		ui_split_leftratio(1);
			ui_split_topratio(1/2);
				slider8 = labelled_dial(slider8, 0, 100, 2, "detuning", "%i cents");
			ui_split_next();
				slider9 = labelled_dial(slider9, 0.1, 10, 0, "chorus speed", "%.1fHz");
			ui_pop();
		ui_pop();
	ui_pop();
) : control_system();

ui_interacted() ? update();
